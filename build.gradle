plugins {
    id 'org.springframework.boot' version '2.5.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
}

group = 'com.github.lhotari.pulsarlogging'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        url 'https://repository.apache.org/snapshots/'
        content {
            includeGroupByRegex("org\\.apache\\..*")
        }
        mavenContent {
            snapshotsOnly()
        }
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-starter-json'
    implementation 'org.springframework.boot:spring-boot-starter-log4j2'
    modules {
        module("org.springframework.boot:spring-boot-starter-logging") {
            replacedBy("org.springframework.boot:spring-boot-starter-log4j2", "Use Log4j2 instead of Logback")
        }
    }
    implementation 'org.apache.logging.log4j:log4j-spring-boot'
    implementation 'org.apache.logging.log4j:log4j-kubernetes'
    implementation 'org.apache.pulsar:pulsar-client:2.8.0'
    implementation('org.apache.pulsar:pulsar-log4j2-appender:2.8.0-SNAPSHOT') {
        // temporary workaround to exclude invalid dependency
        exclude group: 'org.apache.pulsar', module: 'buildtools'
    }
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.projectreactor:reactor-test'
}

test {
    useJUnitPlatform()
}